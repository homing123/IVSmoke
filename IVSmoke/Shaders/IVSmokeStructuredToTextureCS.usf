#include "/Engine/Private/Common.ush"
#include "IVSmokeCommon.ush"

RWTexture3D<float> Desti;
StructuredBuffer<float> BirthTimes;
StructuredBuffer<float> DeathTimes;
StructuredBuffer<FVolumeGPUData> VolumeDataBuffer;

int3 TexSize;
int3 VoxelResolution;
int PackedInterval;
float GameTime;

bool IsValidTime(float t)
{
	return t >= 0.001 && t <= GameTime;
}

float GetExpansionCurve(float t)
{
	return pow(saturate(t), 0.5);
}

float GetDissipationCurve(float t)
{
	return 1.0 - pow(saturate(t), 0.5);
}

[numthreads(8, 8, 8)]
void MainCS(uint3 DispatchThreadId : SV_DispatchThreadID)
{
	uint3 PixelCoord = DispatchThreadId.xyz;
	if (PixelCoord.x >= TexSize.x || PixelCoord.y >= TexSize.y || PixelCoord.z >= TexSize.z)
	{
		return;
	}

	int SliceHeight = VoxelResolution.z + PackedInterval;
	uint VolumeIndex = PixelCoord.z / SliceHeight;

	int LocalZ = PixelCoord.z % SliceHeight;
	if (LocalZ >= VoxelResolution.z)
	{
		Desti[PixelCoord] = 0.0f;
		return;
	}

	FVolumeGPUData VolumeData = VolumeDataBuffer[VolumeIndex];

	uint LocalIdx = PixelCoord.x + VoxelResolution.x * PixelCoord.y + VoxelResolution.x * VoxelResolution.y * LocalZ;
	uint SourceIdx = VolumeData.VoxelBufferOffset + LocalIdx;

	float BirthTime = BirthTimes[SourceIdx];
	if (!IsValidTime(BirthTime))
	{
		Desti[PixelCoord] = 0.0f;
		return;
	}

	float ExpansionProgress = saturate((GameTime - BirthTime) / VolumeData.FadeInDuration);
	float ExpansionDensity = GetExpansionCurve(ExpansionProgress);

	float DeathTime = DeathTimes[SourceIdx];
	float DissipationDensity = 1.0f;
	if (IsValidTime(DeathTime))
	{
		float DissipationProgress = saturate((GameTime - DeathTime) / VolumeData.FadeOutDuration);
		DissipationDensity = GetDissipationCurve(DissipationProgress);
	}

	Desti[PixelCoord] = ExpansionDensity * DissipationDensity;
}
